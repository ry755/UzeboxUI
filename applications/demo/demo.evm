array8u align[3];	// this is used to move everything forward by 3 bytes
					// so a jump instruction can be inserted later. don't move it!

function createWindow(locationX,locationY,sizeX,sizeY,title,titleSize) {
	local i;
	for (i=0; $ptr8u[title, i]; i++)
		$uf0($ptr8u[title, i]);
	$uf1(locationX,locationY,sizeX,sizeY,titleSize);
	$uf0(); // call with no args to reset text buffer
}

function printWindow(locationX,locationY,windowNumber,text,textSize,color) {
	local i;
	for (i=0; $ptr8u[text, i]; i++)
		$uf0($ptr8u[text, i]);
	$uf2(locationX,locationY,windowNumber,textSize,color);
	$uf0(); // call with no args to reset text buffer
}

function setWindowTile(locationX,locationY,windowNumber,tile) {
	$uf5(locationX,locationY,windowNumber,tile);
}

function clearWindow(windowNumber,tile) {
	$uf4(windowNumber,tile);
}

function main() {
	local x,dx,y,dy,tile,dtile,num,delay;
	x = 0;
	y = 0;
	tile = 36;
	dx = 1;
	dy = 2;
	dtile = 1;
	createWindow(5,5,10,10,"hello!!!",8);
	num = $uf3(); // get window number
	clearWindow(num,0);
	printWindow(0,1,num,"Hi!",3,1);
	printWindow(0,3,num,"This is",7,1);
	printWindow(0,4,num,"running in",10,1);
	printWindow(0,5,num,"EmbedVM!",8,1);
	for (delay=175; delay>0; delay--){} // wait for a bit
	clearWindow(num,3);
	while(1) {
		setWindowTile(x,y,num,tile);
		if (x >= 9) dx = -1;
		if (x == 0) dx = 1;
		if (y >= 9) dy = -1;
		if (y == 0) dy = 2;
		if (tile == 40) dtile = -1;
		if (tile == 36) dtile = 1;
		x += dx;
		y += dy;
		tile += dtile;
	}
}
